plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace 'com.yidont.library.utils'
    compileSdk 33

    defaultConfig {
        minSdk 23
        //noinspection OldTargetApi
        targetSdk 31

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    compileOnly 'androidx.appcompat:appcompat:1.6.1'
}

afterEvaluate {
    publishing {
        publications {
            uploadToMavenCentral(MavenPublication) {
                groupId = 'io.github.zwonb'
                artifactId = 'utils'
                version = '0.5'
                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
                pom {
                    name = "Utils"
                    description = "Android library utils"
                    url = "https://github.com/zwonb/utils"
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'zwonb'
                            name = 'zhouyuanbin'
                            email = '1140114702@qq.com'
                        }
                    }
                    scm {
                        connection = "https://github.com/zwonb/utils.git"
                        developerConnection = "https://github.com/zwonb/utils.git"
                        url = "https://github.com/zwonb/utils"
                    }
                }
            }
        }
        repositories {
            maven {
                url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                credentials {
                    try {
                        username = ossrhUsername
                        password = ossrhPassword
                    } catch (ignored) {
                    }
                }
            }
//            maven {
//                url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
//                credentials {
//                    username = ossrhUsername
//                    password = ossrhPassword
//                }
//            }
        }
    }
}

signing {
    sign(publishing.publications)
}